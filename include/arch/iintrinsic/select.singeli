local {
  include 'skin/cop'
  include 'util/kind'
  oper ~~ reinterpret infix right 55
  def base{b,{}} = 0; def base{b,{h,...t}} = h + b*base{b,t}
  def all{t} = is{t, 0 <= t}

  def fmt_p{T} = {
    if (isfloat{T}) {
      if (width{T}==32) 'ps' else 'pd'
    } else {
      merge{'epi', fmtnat{width{T}}} # No unsigned instructions here
    }
  }
  def intrin_g{get_typ}{name, V} = {
    def w = width{V}
    def fw = if (w<=128) '' else fmtnat{w}
    merge{'_mm', fw, '_', name, '_', get_typ{V}}
  }
  def intrin = intrin_g{{[_]T} => fmt_p{T}}
  def intrin_b = intrin_g{{V=[_]T} => {
    if (isint{T}) merge{'si',fmtnat{width{V}}} else fmt_p{T}
  }}

  # Only need to cast if element type class changes
  def class{T} = if (isfloat{T}) T else i8  # i8 represents any int
  def call_cast_sub{do_uncast, gen, T, V=[_]E, ...vs} = {
    def uncast{v} = if (do_uncast) V~~v else v
    if (class{E} == class{T}) gen{...vs}
    else uncast{gen{...each{~~{[width{V}/width{T}]T, .}, vs}}}
  }
  def call_cast = call_cast_sub{1, ...}
}

# 16-bit shuffles on half-words don't quite fit vec_shuffle
local def vec_shuffle16_impl{suff, vec:V, ind} = {
  emit{V, intrin{merge{'shuffle', suff}, [width{V}/16]i16}, vec, base{4, ind}}
}
local def has_sh16{w} = (w==128 and hasarch{'SSE2'}) or (w==256 and hasarch{'AVX2'})
local def is_bool = match { {0}=>1; {1}=>1; {_}=>0 }
def vec_shuffle16_half{half, vec:V=[_]_, {...ind} if is_bool{half} and has_sh16{width{V}} and length{ind}==4 and all{(ind>=0) & (ind<4)}} = {
  def part = select{tup{'lo','hi'}, half}
  call_cast{vec_shuffle16_impl{part, ., ind}, i16, V, vec}
}
def vec_shuffle16_lo{...} = vec_shuffle16_half{0, ...}
def vec_shuffle16_hi{...} = vec_shuffle16_half{1, ...}

# General implementation for constant indices
local def vec_shuffle_impl{T, sel_n, val={_:V, ..._}, ind} = {
  def pos = all{ind >= 0}
  def w = width{T}
  def wv= width{V}
  def sel_width = sel_n * w
  def ind_width = length{ind} * w
  def 0 = wv % ind_width
  # Index expansion and instruction calls
  def get_ind{sel_n, n, ind} = {
    def exp{i} = if (n <= length{i}) i else exp{merge{i, i}}
    def off{i} = i + (range{length{i}} & -sel_n) * (i >= 0)
    off{exp{ind}}
  }
  def shuf_sub{I, name, ind, val} = {
    emit{V, intrin{name, [wv/width{I}]I}, ...val, ind}
  }
  def shuf{name, n, fmt} = shuf_sub{T, name, fmt{get_ind{sel_n, n, ind}}, val}
  def shuf{name, E, n, fmt} = {
    def e = w / width{E}
    def wi = if (e == 1) ind else merge{...each{+{.,range{e}}, e*ind}}
    shuf_sub{E, name, fmt{get_ind{e*sel_n, n, wi}}, val}
  }
  def shuf_base{...a, n   } = shuf{...a, n, base{n, .}}
  def shuf_make{...a, n, T} = shuf{...a, n, vec_make{[n]T, .}}
  # Arch-specific cases
  if (sel_n == 1 and 1 == length{val}) {
    select{val, 0}
  } else if (2 == length{val}) {
    def 1 = isfloat{T} and wv >= 128 and ind_width <= 128 and pos
    shuf_base{'shuffle', 128 / w}
  } else if (wv < 128) {
    def 1 = wv == 64 and isint{T} and w >= 16 and pos
    shuf_base{'shuffle', 4}
  } else if (sel_width == 256) {
    def 1 = hasarch{'AVX2'} and pos
    match (w) {
      {32} => shuf_make{'permutevar8x32', 8, u32}
      {64} => shuf_base{'permute4x64', 4}
    }
  } else if (isfloat{T}) {
    def 1 = pos  # Zeroing unsupported for float selection
    if (ind_width == 256) {
      def 1 = hasarch{'AVX'}
      if (sel_width == 128 and w == 64) {
        shuf{'permute', 2, {t} => base{2, t%2}}
      } else {
        def n = 256 / w
        def U = [n]primtype{'u', w}
        shuf{'permutevar', n, {i} => vec_make{U, (w/32)*i}}
      }
    } else {
      def n = 128 / w
      if (hasarch{'AVX'}) shuf{'permute', n, {t} => base{n, if (n==2 and wv>128) merge{t,t} else t}}
      else shuf_sub{T, 'shuffle', base{n, get_ind{sel_n, n, ind}}, merge{val,val}}
    }
  } else { # isint{T}
    def 1 = hasarch{'SSE2'}
    match () {
      {if w >= 32 and pos}  => shuf_base{'shuffle', i32, 4}
      {if hasarch{'SSSE3'}} => shuf_make{'shuffle', i8, wv/8, i8}
      {if w >= 16 and sel_width <= 64 and pos} => {
        def io = get_ind{w/16 * sel_n, 4, ind}
        def i = io - (range{length{io}} & -4) # Avoid passing offset indices to shufflehi
        def fi{v, s, ...bnd} = vec_shuffle16_impl{s, v, slice{i, ...bnd}}
        fi{fi{...val, 'lo',0,4}, 'hi',-4}
      }
    }
  }
}

# Process parameters
local def vec_shuffle_proc{...spec, V=[_]vT, vals, {...inds}} = {
  # inds doesn't have the element type so it comes from spec or val
  def T = match (...spec) {
    {[_]T} => T; {T if ktyp{T}} => T
    {n if knum{n}} => vT; {} => vT
  }
  # sel_n is the number of indices in a selection unit
  # It can be specified by spec as a number or length of vector type
  # and/or by the length of each list in nested inds
  def spec_n = match (...spec) {
    {[k]_} => tup{k}; {k if knum{k}} => tup{k}; {..._} => tup{}
  }
  def {ind_n, ind} = match (inds) {
    {{{...t}, ..._}} => {
      def l = length{t}
      def 1 = all{l == each{length, inds}}
      tup{tup{l}, merge{...inds}}
    }
    {_} => {
      each{{s} => { def 0 = length{inds} % s }, spec_n}
      tup{tup{}, inds}
    }
  }
  def sel_n = match(...spec_n, ...ind_n) {  # Shuffle unit from spec and nested indices must match
    {n,n} => n; {n} => n; {} => length{ind}
  }
  def 1 = all{ind < sel_n}
  def shuf{...v} = vec_shuffle_impl{T, sel_n, v, ind}
  call_cast{shuf, T, V, ...vals}
}

# For convenience, allow indices to be written directly without tup
def vec_select {val, ...inds if 1 < length{inds}} = vec_select {val, inds}
def vec_shuffle{val, ...inds if 1 < length{inds}} = vec_shuffle{val, inds}
def vec_select {spec if knum{spec} or ktyp{spec}, val, ...inds if 1 < length{inds}} = vec_select {spec, val, inds}
def vec_shuffle{spec if knum{spec} or ktyp{spec}, val, ...inds if 1 < length{inds}} = vec_shuffle{spec, val, inds}
# Main definitions
def vec_shuffle{...spec, val:    V=[_]_      , {...inds}} = vec_shuffle_proc{...spec, V, tup{val}, inds}
def vec_shuffle{...spec, vals={_:V=[_]_, _:V}, {...inds}} = vec_shuffle_proc{...spec, V, vals,     inds}

# Variable indices: not many cases to support
def vec_shuffle{...spec, val:V=[vk]vT, ind:[ik]I if isint{I}} = {
  # Selection type and size can be set by spec, or come from ind
  # But element quality comes from val if possible
  def wi = width{I}
  def ty{} = if (wi < 32) I else primtype{quality{vT}, wi}
  def {sel_n, T} = match (...spec) {
    {[n]T}         => tup{ n,T}
    {T if ktyp{T}} => tup{ik,T}
    {n if knum{n}} => tup{ n,ty{}}
    {}             => tup{ik,ty{}}
  }
  def 1 = wi == width{T}
  def 0 = ik % sel_n
  def name = match (sel_n, width{T}) {
    {16, 8 if hasarch{'SSSE3'}} => 'shuffle'
    {4, 32 if hasarch{'AVX'} and isfloat{T}} => 'permutevar'
    # no 2, 64: permutevar_pd scales indices!
    {8, 32 if hasarch{'AVX2'}} => 'permutevar8x32'
  }
  def S = [width{V}/width{T}]T
  def shuf = emit{., intrin{name, S}, ., ind}
  if (class{T} == class{vT}) shuf{V, val}
  else                  V ~~ shuf{S, S ~~ val}
}

# Next-to-last index bit is used, so index has to be multiplied by 2
def vec_shuffle_64_scaled{val:V=[k](f64), ind:[k]I if isint{I} and width{I}==64 and hasarch{'AVX'}} = {
  emit{V, intrin{'permutevar_pd', V}, val, ind}
}
def vec_shuffle_64_scaled{val:V=[_]T, ind if T != f64} = {
  V ~~ vec_shuffle_64_scaled{[width{V}/64]f64~~val, ind}
}

def vec_select{...spec, v:V=[_]vT, ind} = {
  def T = match (...spec) {
    {} => vT; {T if ktyp{T}} => T
    {w if knum{w} and w<=64} => primtype{if (w<32 and isfloat{vT}) 'u' else quality{vT}, w}
  }
  def w = width{T}
  def k = width{V} / w
  def 1 = match (ind) { {{...t}} => length{t}==k; {i:[(k)]I} => isint{I} }
  vec_shuffle{T, v, ind}
}
def vec_select{n, v:V=[_]vT, {...ind} if knum{n} and n>64} = {
  def e = n / 64
  vec_select{64, v, merge{...each{+{.,range{e}}, e*ind}}}
}
def vec_select{(width{V}), x:V=[_]_, i if is{i,0} or is{i,tup{0}}} = x
# Selects as 4 unified lanes
def vec_select{128, v={a:V=[_]T, b:V}, ind={_,_} if 256==width{V} and hasarch{'AVX'} and all{(ind>=0) & (ind<4)}} = {
  def q = if (isint{T} and hasarch{'AVX2'}) 'x' else 'f'
  def name = merge{'permute2', q, '128'}
  emit{V, intrin_b{name, V}, a, b, base{16, ind}}
}

# Reverse-units for compatibility with NEON
def reverse_units{n, x:[l]_ if knum{n} and n>1 and l%n == 0} = {
  vec_shuffle{x, n-1 - range{n}}
}


# Zip
local def has_zip{V=[_]T} = {
  hasarch{match (width{V}) {
    {128} => if (T==f32) 'SSE' else 'SSE2'
    {256} => if (isfloat{T}) 'AVX' else 'AVX2'
    {512} => if (width{T}>=32) 'AVX512F' else 'AVX512BW'
  }}
}
def zip128{a:V=[_]_, b:V, half if has_zip{V}} = {
  def name = merge{'unpack', match (half) { {0}=>'lo'; {1}=>'hi' }}
  emit{V, intrin{name, V}, a, b}
}
def zip{a:V=[_]_, b:V, half if width{V}==128} = zip128{a, b, half}


# Shift/align as a list of elements
# Directions left and right are opposite to instruction l and r!
local {
  def has_sh{w, sse} = hasarch{match (w) { {128}=>sse; {256}=>'AVX2'; {512}=>'AVX512BW' }}
  def has_sh{V=[_]T, sse, n} = isint{T} and has_sh{width{V}, sse} and knum{n}
  # Shift left or right based on sign
  def vec_shift{w128, dir}{x:V=[_]T, n if has_sh{V,'SSE2',n} and (w128 or width{V}==128)} = {
    def wv = width{V}; def S = [width{V}/8]u8
    def name = merge{'bs', if (dir*n < 0) 'l' else 'r', 'li'}
    def suff = if (wv == 128) 'si128' else 'epi128'
    def nb = __min{16, __abs{n}*(width{T}/8)}  # Shift in bytes
    def sh = emit{V, intrin_g{{_} => suff}{name, S}, ..., nb}
    call_cast{sh, T, S, x}
  }
  # Merge-shift, with mod_n to pick direction
  def vec_merge_shift{w128, mod_n}{a:V=[k]T, b:V, n if has_sh{V,'SSSE3',n} and (w128 or width{V}==128)} = {
    def S = [width{V}/8]u8
    def sh = emit{V, intrin{'alignr', S}, ..., mod_n{n*(width{T}/8)}}
    call_cast{sh, T, S, b, a}
  }
}
def vec_shift_left_128 {...} = vec_shift{1,  1}
def vec_shift_left     {...} = vec_shift{0,  1}
def vec_shift_right_128{...} = vec_shift{1, -1}
def vec_shift_right    {...} = vec_shift{0, -1}

def vec_merge_shift_left_128 {...} = vec_merge_shift{1, {n} => n}
def vec_merge_shift_left     {...} = vec_merge_shift{0, {n} => n}
def vec_merge_shift_right_128{...} = vec_merge_shift{1, {n} => 16 - n}
def vec_merge_shift_right    {...} = vec_merge_shift{0, {n} => 16 - n}


# Blend
# x86 vector blends use the top bit of the mask
# But blend_hom is more general so blend_top is defined as a restriction
local {
  def has_blend{V=[_]T} = match (width{V}) {
    {128} => hasarch{'SSE4.1'}
    {256} => hasarch{if (isfloat{T}) 'AVX' else 'AVX2'}
    {_} => 0
  }
  def has_blendv{V, M} = width{V}==width{M} and has_blend{V}
  def blend_instr{hom}{f:V=[_]T, t, m:M if has_blendv{V, M} and (hom or isfloat{T} or width{T}==8)} = {
    def name = intrin{'blendv', if (isfloat{T}) V else [width{V}/8]i8}
    call_cast_sub{0, emit{V, name, f, t, ...}, T, M, m}
  }
}
def blend_hom{...} = blend_instr{1}
def blend_top{...} = blend_instr{0}

# Blend with immediate
local def all_bool{m} = all{each{knum, m}} and all{(m==0) | (m==1)}
def blend_units{f, t, ...m if all_bool{m}} = blend_units{f, t, m}
def blend_units{f:V=[k]T, t:V, {...m} if has_blend{V} and width{T}>=16 and 0 == __min{8,k} % length{m} and all_bool{m}} = {
  def E = {
    if (isfloat{T}) T
    else if (width{T}>=32 and hasarch{'AVX2'}) i32
    else i16
  }
  def l = __min{8,k}
  def exp{i} = if (l <= length{i}) i else exp{merge{i, i}}
  def c = width{T} / width{E}  # Copy each bit of m c times
  def b = 1 << c
  emit{V, intrin{'blend', [k*c]E}, f, t, base{b, exp{m}*(b-1)}}
}
def blend{f, t, ...m if all_bool{m}} = blend{f, t, m}
def blend{f:V=[k]T, t:V, {...m} if has_blend{V} and k<=8 and length{m}==k} = {
  blend_units{f, t, m}
}
